(define (make-mutex)
  (let ((cell (list false)))
    (define (the-mutex m)
      (cond ((eq? m 'acquire)
             (if (test-and-set! cell)
                 (the-mutex 'acquire)))
            ((eq? m 'release)
             (clear! cell))))
    the-mutex))

(define (clear! cell)
  (set-car! cell false))

(define (test-and-set! cell)
  (if (car cell)
      true
      (begin (set-car! cell true)
             false)))


;; a. in terms of mutexes
(define (make-semaphore n)
  (let ((mutex (make-mutex))
        (count 0))
    (define (acquire)
      (mutex 'acquire)
      (if (= count n)   ; if already have n processes acquiring the semaphore
          (begin (mutex 'release)
                 (acquire))    ; acquire again
          (begin (set! count (+ count 1))
                 (mutex 'release))))
    (define (release)
      (mutex 'acquire)
      (set! count (- count 1))
      (mutex 'release))
    (define (the-semaphore m)
      (cond ((eq? m 'acquire)
             (acquire))
            ((eq? m 'release)
             (release))))
    the-semaphore))


;; b. in terms of atomic test-and-set! operations
(define (make-semaphore n)
  (let ((cell (list false))
        (count 0))
    (define (acquire)
      (acquire-cell)
      (if (= count n)
          (begin (clear! cell)
                 (acquire))
          (begin (set! count (+ count 1))
                 (clear! cell))))
    (define (release)
      (acquire-cell)
      (set! count (- count 1))
      (clear! cell))
    (define (acquire-cell)
      (if (test-and-set! cell)
          (acquire-cell)))
    (define (the-semaphore m)
      (cond ((eq? m 'acquire)
             (acquire))
            ((eq? m 'release)
             (release))))
    the-semaphore))
